---
layout: "post"
title: "「PLFA」 10 Lists"
subtitle: "列表"
author: "roife"
date: 2023-01-13

tags: ["Agda@编程语言", "读书笔记@Tags", "Programming Language Foundations in Agda@读书笔记", "Dependent Type@程序语言理论", "形式化验证@程序语言理论", "类型系统@程序语言理论", "程序语言理论@Tags"]
lang: zh
catalog: true
header-image: ""
header-style: text
---

# 列表

## 定义

```agda
data List (A : Set) : Set where
  []  : List A              -- 读作 nil
  _∷_ : A → List A → List A -- 读作 cons

infixr 5 _∷_
```

Indices 可以被转换成 parameters。上面的定义与下列等价：

```agda
data List′ : Set → Set where
  []′  : ∀ {A : Set} → List′ A
  _∷′_ : ∀ {A : Set} → A → List′ A → List′ A
```

通过下面这条指令，可以告诉编译器我们定义的 `List` 等价于 Haskell 的 `List`，这样 agda 在处理时可以直接用 Haskell 的 `List` 来处理：

```agda
{-# BUILTIN LIST List #-}
```

## 表示

使用下面的语法可以更好地表示列表：

```agda
pattern [_] z = z ∷ []
pattern [_,_] y z = y ∷ z ∷ []
pattern [_,_,_] x y z = x ∷ y ∷ z ∷ []
pattern [_,_,_,_] w x y z = w ∷ x ∷ y ∷ z ∷ []
pattern [_,_,_,_,_] v w x y z = v ∷ w ∷ x ∷ y ∷ z ∷ []
pattern [_,_,_,_,_,_] u v w x y z = u ∷ v ∷ w ∷ x ∷ y ∷ z ∷ []
```

例如 `[x, y]` 等价于 `x :: y :: []`。

# `_++_`

```agda
infixr 5 _++_

_++_ : ∀ {A : Set} → List A → List A → List A
[]       ++ ys  =  ys
(x ∷ xs) ++ ys  =  x ∷ (xs ++ ys)
```

这里的 `A` 是一个参数，并且 `_++_` 是一个 **polymorphic** dependent function。

```agda
_ : [ 0 , 1 , 2 ] ++ [ 3 , 4 ] ≡ [ 0 , 1 , 2 , 3 , 4 ]
_ =
  begin
    0 ∷ 1 ∷ 2 ∷ [] ++ 3 ∷ 4 ∷ []
  ≡⟨⟩
    0 ∷ (1 ∷ 2 ∷ [] ++ 3 ∷ 4 ∷ [])
  ≡⟨⟩
    0 ∷ 1 ∷ (2 ∷ [] ++ 3 ∷ 4 ∷ [])
  ≡⟨⟩
    0 ∷ 1 ∷ 2 ∷ ([] ++ 3 ∷ 4 ∷ [])
  ≡⟨⟩
    0 ∷ 1 ∷ 2 ∷ 3 ∷ 4 ∷ []
  ∎
```

## 性质

由于列表和自然数同构，因此二者具有类似的性质和证明过程。

### 结合律

```agda
++-assoc : ∀ {A : Set} (xs ys zs : List A)
  → (xs ++ ys) ++ zs ≡ xs ++ (ys ++ zs)

++-assoc [] ys zs =
  begin
    ([] ++ ys) ++ zs
  ≡⟨⟩
    ys ++ zs
  ≡⟨⟩
    [] ++ (ys ++ zs)
  ∎

++-assoc (x ∷ xs) ys zs =
  begin
    (x ∷ xs ++ ys) ++ zs
  ≡⟨⟩
    x ∷ (xs ++ ys) ++ zs
  ≡⟨⟩
    x ∷ ((xs ++ ys) ++ zs)
  ≡⟨ cong (x ∷_) (++-assoc xs ys zs) ⟩
    x ∷ (xs ++ (ys ++ zs))
  ≡⟨⟩
    x ∷ xs ++ (ys ++ zs)
  ∎
```

### 幺元

```agda
++-identityˡ : ∀ {A : Set} (xs : List A) → [] ++ xs ≡ xs
++-identityˡ xs =
  begin
    [] ++ xs
  ≡⟨⟩
    xs
  ∎

-----------

++-identityʳ : ∀ {A : Set} (xs : List A) → xs ++ [] ≡ xs

++-identityʳ [] =
  begin
    [] ++ []
  ≡⟨⟩
    []
  ∎

++-identityʳ (x ∷ xs) =
  begin
    (x ∷ xs) ++ []
  ≡⟨⟩
    x ∷ (xs ++ [])
  ≡⟨ cong (x ∷_) (++-identityʳ xs) ⟩
    x ∷ xs
  ∎
```

# `length`

```agda
length : ∀ {A : Set} → List A → ℕ
length []        =  zero
length (x ∷ xs)  =  suc (length xs)
```

### `length-++`

```agda
length-++ : ∀ {A : Set} (xs ys : List A)
  → length (xs ++ ys) ≡ length xs + length ys

length-++ {A} [] ys =
  begin
    length ([] ++ ys)
  ≡⟨⟩
    length ys
  ≡⟨⟩
    length {A} [] + length ys
  ∎

length-++ (x ∷ xs) ys =
  begin
    length ((x ∷ xs) ++ ys)
  ≡⟨⟩
    suc (length (xs ++ ys))
  ≡⟨ cong suc (length-++ xs ys) ⟩
    suc (length xs + length ys)
  ≡⟨⟩
    length (x ∷ xs) + length ys
  ∎
```

此处第一种情况中 agda 无法推断出类型参数，所以需要手动加上。

# `reverse`

```agda
reverse : ∀ {A : Set} → List A → List A
reverse []        =  []
reverse (x ∷ xs)  =  reverse xs ++ [ x ]
```

但是这个操作非常慢，时间复杂度为 $O(n^2)$。

## 性质

### `reverse-++-distrib`

`reverse` 对 `_++_` 分配：

```agda
reverse-++-distrib : ∀ {A : Set} → (xs ys : List A)
  → reverse (xs ++ ys) ≡ reverse ys ++ reverse xs
reverse-++-distrib [] [] = refl
reverse-++-distrib (x ∷ xs) [] rewrite (reverse-++-distrib xs []) = refl
reverse-++-distrib [] (y ∷ ys) rewrite (++-identityʳ (reverse ys ++ [ y ])) = refl
reverse-++-distrib (x ∷ xs) (y ∷ ys) rewrite (reverse-++-distrib xs (y ∷ ys))
                                           | (++-assoc (reverse ys ++ [ y ]) (reverse xs) [ x ])= refl
```

### `reverse-involutive`

两次 `reverse` 后可以得到自身：

```agda
reverse-involutive : ∀ {A : Set} → (xs : List A)
  → reverse (reverse xs) ≡ xs
reverse-involutive [] = refl
reverse-involutive (x ∷ xs) rewrite (reverse-++-distrib (reverse xs) [ x ])
                                  | (reverse-involutive xs) = refl
```

## `shunt`

`shunt` 是 `reverse` 的推广，它会将第一个参数倒序接在第二个参数上：

```agda
shunt : ∀ {A : Set} → List A → List A → List A
shunt []       ys  =  ys
shunt (x ∷ xs) ys  =  shunt xs (x ∷ ys)
```

此处实际上用了尾递归，时间复杂度为 $O(n)$。因此如果用 `shunt` 来定义 `reverse`，那就能得到一个更快的 `reverse`。

下面证明了 `reverse` 和 `shunt` 的关系，并重新定义了 `reverse`：

```agda
shunt-reverse : ∀ {A : Set} (xs ys : List A)
  → shunt xs ys ≡ reverse xs ++ ys
shunt-reverse [] ys =
  begin
    shunt [] ys
  ≡⟨⟩
    ys
  ≡⟨⟩
    reverse [] ++ ys
  ∎
shunt-reverse (x ∷ xs) ys =
  begin
    shunt (x ∷ xs) ys
  ≡⟨⟩
    shunt xs (x ∷ ys)
  ≡⟨ shunt-reverse xs (x ∷ ys) ⟩
    reverse xs ++ (x ∷ ys)
  ≡⟨⟩
    reverse xs ++ ([ x ] ++ ys)
  ≡⟨ sym (++-assoc (reverse xs) [ x ] ys) ⟩
    (reverse xs ++ [ x ]) ++ ys
  ≡⟨⟩
    reverse (x ∷ xs) ++ ys
  ∎
```

```agda
reverse′ : ∀ {A : Set} → List A → List A
reverse′ xs = shunt xs []

reverses : ∀ {A : Set} (xs : List A)
  → reverse′ xs ≡ reverse xs
reverses xs =
  begin
    reverse′ xs
  ≡⟨⟩
    shunt xs []
  ≡⟨ shunt-reverse xs [] ⟩
    reverse xs ++ []
  ≡⟨ ++-identityʳ (reverse xs) ⟩
    reverse xs
  ∎
```

# `map`

```agda
map : ∀ {A B : Set} → (A → B) → List A → List B
map f []        =  []
map f (x ∷ xs)  =  f x ∷ map f xs
```

利用 currying 可以将一个作用于元素的函数转换为作用于列表的函数：

```agda
sucs : List ℕ → List ℕ
sucs = map suc
```

## 性质

### `map-compose`

函数组合的映射是两个映射的组合：

```agda
map-compose : ∀ {A B C : Set} {f : A → B} {g : B → C}
  → map (g ∘ f) ≡ map g ∘ map f
map-compose {A} {B} {C} {f} {g} = extensionality (lemma f g)
  where
    lemma : ∀ {A B C : Set} (f : A → B) (g : B → C) (xs : List A)
      → map (g ∘ f) xs ≡ (map g ∘ map f) xs
    lemma f g [] = refl
    lemma f g (x ∷ xs) = cong ((g ∘ f) x ∷_) (lemma f g xs)
```

### `map-++-distribute`

映射对列表存在分配律：

```agda
map-++-distribute : ∀ {A B : Set} → (f : A → B) → (xs ys : List A)
  → map f (xs ++ ys) ≡ map f xs ++ map f ys
map-++-distribute f [] ys = refl
map-++-distribute f (x ∷ xs) ys rewrite (map-++-distribute f xs ys) = refl
```

# `foldr`

```agda
foldr : ∀ {A B : Set} → (A → B → B) → B → List A → B
foldr _⊗_ e []        =  e
foldr _⊗_ e (x ∷ xs)  =  x ⊗ foldr _⊗_ e xs
```

同样，`fold` 也可以应用 currying 来获得新函数：

```agda
sum : List ℕ → ℕ
sum = foldr _+_ 0
```

## 性质

### `fold-++`

对于 `_++_` 进行 `foldr`，其过程可以拆成两部分。

```agda
foldr-++ : ∀ {A B : Set} → (_⊗_ : A → B → B) → (e : B) → (xs ys : List A)
  → foldr _⊗_ e (xs ++ ys) ≡ foldr _⊗_ (foldr _⊗_ e ys) xs
foldr-++ _⊗_ e [] ys = refl
foldr-++ _⊗_ e (x ∷ xs) [] rewrite (++-identityʳ xs) = refl
foldr-++ _⊗_ e (x ∷ xs) (y ∷ ys) rewrite (foldr-++ _⊗_ e xs (y ∷ ys)) = refl
```

### `foldr-∷`

`_++_` 就是 `foldr _∷_`：

```agda
foldr-∷ : ∀ {A : Set} → (xs ys : List A)
  → xs ++ ys ≡ foldr _∷_ ys xs
foldr-∷ [] ys = refl
foldr-∷ (x ∷ xs) ys = cong (x ∷_) (foldr-∷ xs ys)
```

### `map-is-foldr`

`map` 可以用 `foldr` 来表达：

```agda
map-is-foldr : ∀ {A B : Set} → (f : A → B → B)
  → map f ≡ foldr (λ x xs → f x ∷ xs) []
map-is-foldr f = extensionality (λ xs → lemma f xs)
  where
    lemma : ∀ {A B : Set} → (f : A → B → B) → (xs : List A)
      → map f xs ≡ foldr (λ x → _∷_ (f x)) [] xs
    lemma f [] = refl
    lemma f (x ∷ xs) = cong (f x ∷_) (lemma f xs)
```

# Monoids

Monoids 在 agda 中的建模如下：

```agda
record IsMonoid {A : Set} (_⊗_ : A → A → A) (e : A) : Set where
  field
    assoc : ∀ (x y z : A) → (x ⊗ y) ⊗ z ≡ x ⊗ (y ⊗ z)
    identityˡ : ∀ (x : A) → e ⊗ x ≡ x
    identityʳ : ∀ (x : A) → x ⊗ e ≡ x

open IsMonoid
```

## 例子

下面是 monoids 的一些例子：

```agda
+-monoid : IsMonoid _+_ 0
+-monoid =
  record
    { assoc = +-assoc
    ; identityˡ = +-identityˡ
    ; identityʳ = +-identityʳ
    }

*-monoid : IsMonoid _*_ 1
*-monoid =
  record
    { assoc = *-assoc
    ; identityˡ = *-identityˡ
    ; identityʳ = *-identityʳ
    }

++-monoid : ∀ {A : Set} → IsMonoid {List A} _++_ []
++-monoid =
  record
    { assoc = ++-assoc
    ; identityˡ = ++-identityˡ
    ; identityʳ = ++-identityʳ
    }
```

## `foldr-monoid`

如果 `_⊗_` 和 `e` 构成一个 monoid，那么 `foldr` 满足下面的性质：

```agda
foldr-monoid : ∀ {A : Set} (_⊗_ : A → A → A) (e : A) → IsMonoid _⊗_ e →
  ∀ (xs : List A) (y : A) → foldr _⊗_ y xs ≡ foldr _⊗_ e xs ⊗ y
foldr-monoid _⊗_ e ⊗-monoid [] y =
  begin
    foldr _⊗_ y []
  ≡⟨⟩
    y
  ≡⟨ sym (identityˡ ⊗-monoid y) ⟩
    (e ⊗ y)
  ≡⟨⟩
    foldr _⊗_ e [] ⊗ y
  ∎
foldr-monoid _⊗_ e ⊗-monoid (x ∷ xs) y =
  begin
    foldr _⊗_ y (x ∷ xs)
  ≡⟨⟩
    x ⊗ (foldr _⊗_ y xs)
  ≡⟨ cong (x ⊗_) (foldr-monoid _⊗_ e ⊗-monoid xs y) ⟩
    x ⊗ (foldr _⊗_ e xs ⊗ y)
  ≡⟨ sym (assoc ⊗-monoid x (foldr _⊗_ e xs) y) ⟩
    (x ⊗ foldr _⊗_ e xs) ⊗ y
  ≡⟨⟩
    foldr _⊗_ e (x ∷ xs) ⊗ y
  ∎
```

### `foldr-monoid-++`

由于 `foldr` 是 monoid，因此对 `_++_` 进行 `foldr`，相当于对两个列表分别 `foldr`，然后再计算。

```agda
foldr-monoid-++ : ∀ {A : Set} (_⊗_ : A → A → A) (e : A) → IsMonoid _⊗_ e →
  ∀ (xs ys : List A) → foldr _⊗_ e (xs ++ ys) ≡ foldr _⊗_ e xs ⊗ foldr _⊗_ e ys
foldr-monoid-++ _⊗_ e monoid-⊗ xs ys =
  begin
    foldr _⊗_ e (xs ++ ys)
  ≡⟨ foldr-++ _⊗_ e xs ys ⟩
    foldr _⊗_ (foldr _⊗_ e ys) xs
  ≡⟨ foldr-monoid _⊗_ e monoid-⊗ xs (foldr _⊗_ e ys) ⟩
    foldr _⊗_ e xs ⊗ foldr _⊗_ e ys
  ∎
```

## `foldl`

```agda
foldl : ∀ {A B : Set} → (B → A → B) → B → List A → B
foldl _⊗_ e [] = e
foldl _⊗_ e (x ∷ xs) = foldl _⊗_ (e ⊗ x) xs
```

### `foldl-monoid`

```agda
foldl-monoid : ∀ {A : Set} (_⊗_ : A → A → A) (e : A) → IsMonoid _⊗_ e →
  ∀ (xs : List A) (y : A) → y ⊗ foldl _⊗_ e xs ≡ foldl _⊗_ y xs
foldl-monoid _⊗_ e ⊗-monoid [] y = identityʳ ⊗-monoid y
foldl-monoid _⊗_ e ⊗-monoid (x ∷ xs) y
    rewrite identityˡ ⊗-monoid x
          | sym (foldl-monoid _⊗_ e ⊗-monoid xs x)
          | sym (assoc ⊗-monoid y x (foldl _⊗_ e xs))
           = foldl-monoid _⊗_ e ⊗-monoid xs (y ⊗ x)
```

### `foldr-monoid-foldl`


如果 `_⊗_` 和 `e` 构成幺半群，那么 `foldr _⊗_ e` 和 `foldl _⊗_ e` 的结果相同。

```agda
foldr-monoid-foldl : ∀ {A : Set} (_⊗_ : A → A → A) (e : A)
  → IsMonoid _⊗_ e
    -------------------------
  → foldr _⊗_ e ≡ foldl _⊗_ e
foldr-monoid-foldl _⊗_ e ⊗-monoid = extensionality (lemma _⊗_ e ⊗-monoid)
  where
    lemma : ∀ {A : Set} (_⊗_ : A → A → A) (e : A)
      → IsMonoid _⊗_ e
        -------------------------------------------------
      → ∀ (xs : List A) → foldr _⊗_ e xs ≡ foldl _⊗_ e xs
    lemma _⊗_ e ⊗-monoid [] = refl
    lemma _⊗_ e ⊗-monoid (x ∷ xs)
        rewrite lemma _⊗_ e ⊗-monoid xs
              | foldl-monoid _⊗_ e ⊗-monoid xs x
              | identityˡ ⊗-monoid x = refl
```

# `All` & `Any`

`All` 和 `Any` 断言了一个列表的性质：前者断言某个谓词对于列表中的所有元素都成立；后者断言某个谓词对列表中的某一元素成立。

## `All`

```agda
data All {A : Set} (P : A → Set) : List A → Set where
  []  : All P []
  _∷_ : ∀ {x : A} {xs : List A} → P x → All P xs → All P (x ∷ xs)
```

`All` 的证明可以用类似列表的方式写：

```agda
_ : All (_≤ 2) [ 0 , 1 , 2 ]
_ = z≤n ∷ s≤s z≤n ∷ s≤s (s≤s z≤n) ∷ []
```

前面针对列表定义的 pattern `[_ , _ , _]` 对于 `All` 也可以使用。（但是需要在 `All` 的定义后面定义 pattern）

### `All-++-≃`

一个谓词对两个连接在一起的列表的每个元素都成立，当且仅当这个谓词对两个列表的每个元素都成立：

```agda
All-++-≃ : ∀ {A : Set} {P : A → Set} (xs ys : List A) →
  All P (xs ++ ys) ≃ (All P xs × All P ys)
All-++-≃ xs ys =
  record
    { to = to xs ys
    ; from = from xs ys
    ; from∘to = from∘to xs ys
    ; to∘from = to∘from xs ys
    }
  where

  to : ∀ {A : Set} {P : A → Set} (xs ys : List A) →
    All P (xs ++ ys) → (All P xs × All P ys)
  to [] ys Pys = ⟨ [] , Pys ⟩
  to (x ∷ xs) ys (Px ∷ Pxs++ys) with to xs ys Pxs++ys
  ... | ⟨ Pxs , Pys ⟩ = ⟨ Px ∷ Pxs , Pys ⟩

  from : ∀ { A : Set} {P : A → Set} (xs ys : List A) →
    All P xs × All P ys → All P (xs ++ ys)
  from [] ys ⟨ [] , Pys ⟩ = Pys
  from (x ∷ xs) ys ⟨ Px ∷ Pxs , Pys ⟩ =  Px ∷ from xs ys ⟨ Pxs , Pys ⟩

  from∘to : ∀ {A : Set} {P : A → Set} (xs ys : List A) (x : All P (xs ++ ys))
    → from xs ys (to xs ys x) ≡ x
  from∘to [] ys x = refl
  from∘to (x ∷ xs) ys (px ∷ allp) = cong (px ∷_) (from∘to xs ys allp)

  to∘from : ∀ {A : Set} {P : A → Set} (xs ys : List A) (y : All P xs × All P ys)
    → to xs ys (from xs ys y) ≡ y
  to∘from [] ys ⟨ [] , snd ⟩ = refl
  to∘from (x ∷ xs) ys ⟨ px ∷ fst , snd ⟩ rewrite to∘from xs ys ⟨ fst , snd ⟩ = refl
```

### `All-∀`

```agda
All-∀ : ∀ {A : Set} {P : A → Set} (xs : List A)
  → All P xs ≃ (∀ x' → x' ∈ xs → P x')
All-∀  xs = record {
       to = to xs;
       from = from  xs ;
       from∘to = from∘to xs ;
       to∘from = to∘from xs
  } where

  to : ∀ {A : Set} {P : A → Set} (xs : List A)
    → All P xs → (∀ x' → x' ∈ xs → P x')
  to xs (Px ∷ Pxs) x' (here x'≡x) rewrite x'≡x = Px
  to (x ∷ xs) (Px ∷ Pxs) x' (there x'∈xs) = to xs Pxs x' x'∈xs

  from : ∀ {A : Set} {P : A → Set} (xs : List A)
    → (∀ x' → x' ∈ xs → P x') → All P xs
  from [] ∀x'Px' = []
  from (x ∷ xs) ∀x'Px' = ∀x'Px' x (here refl)
                         ∷ from xs λ v v∈xs → ∀x'Px' v (there v∈xs)

  from∘to : ∀ {A : Set} {P : A → Set} (xs : List A) (z : All P xs)
    → from xs (to xs z) ≡ z
  from∘to [] [] = refl
  from∘to (x ∷ xs) (Px ∷ Pxs) = cong (Px ∷_) (from∘to xs Pxs)

  to∘from : ∀ {A : Set} {P : A → Set} (xs : List A) (z : ∀ x' → x' ∈ xs → P x')
    → to xs (from xs z) ≡ z
  to∘from xs ∀x'Px' = ∀-extensionality λ x' → extensionality (lemma xs ∀x'Px')
    where
      lemma : ∀ {A : Set} {P : A → Set} {x'} (xs : List A) (z : ∀ x' → x' ∈ xs → P x')
        → (x : x' ∈ xs) → to xs (from xs z) x' x ≡ z x' x
      lemma [] ∀x'Px' ()
      lemma xs ∀x'Px' (here x'≡x) rewrite x'≡x = refl
      lemma (x ∷ xs) ∀x'Px' (there x'∈xs) = lemma xs (λ v v∈xs → ∀x'Px' v (there v∈xs)) x'∈xs
```

## `Any`

```agda
data Any {A : Set} (P : A → Set) : List A → Set where
  here  : ∀ {x : A} {xs : List A} → P x → Any P (x ∷ xs)
  there : ∀ {x : A} {xs : List A} → Any P xs → Any P (x ∷ xs)
```

### `_∈_` & `_∉_`

```agda
infix 4 _∈_ _∉_

_∈_ : ∀ {A : Set} (x : A) (xs : List A) → Set
x ∈ xs = Any (x ≡_) xs

_∉_ : ∀ {A : Set} (x : A) (xs : List A) → Set
x ∉ xs = ¬ (x ∈ xs)
```

`Any` 的证明也和列表很像，其中 `here` 表示此处的元素满足条件，`refl` 为相等的证明。

```agda
_ : 0 ∈ [ 0 , 1 , 0 , 2 ]
_ = here refl

_ : 0 ∈ [ 0 , 1 , 0 , 2 ]
_ = there (there (here refl))
```

```agda
not-in : 3 ∉ [ 0 , 1 , 0 , 2 ]
not-in (here ())
not-in (there (here ()))
not-in (there (there (here ())))
not-in (there (there (there (here ()))))
not-in (there (there (there (there ()))))
```

`()` 出现了五次，分别表示没有 `3 ≡ 0`、`3 ≡ 1`、`3 ≡ 0`、`3 ≡ 2` 和 `3 ∈ []` 的证明。


### `Any-++-⇔`

一个谓词对两个连接在一起的列表中的某个元素成立，当且仅当这个谓词对两个列表的其中一个列表中的一个元素成立：

```agda
Any-++-⇔ : ∀ {A : Set} {P : A → Set} (xs ys : List A) →
  Any P (xs ++ ys) ⇔ (Any P xs ⊎ Any P ys)
Any-++-⇔ xs ys =
  record
    { to = to xs ys
    ; from = from xs ys
    }
  where

    to : ∀ {A : Set} {P : A → Set} (xs ys : List A) →
      Any P (xs ++ ys) → (Any P xs ⊎ Any P ys)
    to [] ys anyp = inj₂ anyp
    to (x ∷ xs) ys (here px) = inj₁ (here px)
    to (x ∷ xs) ys (there anyp) with to xs ys anyp
    ... | inj₁ anyx = inj₁ (there anyx)
    ... | inj₂ anyy = inj₂ anyy

    from : ∀ {A : Set} {P : A → Set} (xs ys : List A) →
      (Any P xs ⊎ Any P ys) → Any P (xs ++ ys)
    from _ _ (inj₁ (here ax)) = here ax
    from (x ∷ xs) ys (inj₁ (there ax)) = there (from xs ys (inj₁ ax))
    from [] ys (inj₂ y) = y
    from (x ∷ xs) ys (inj₂ y) = there (from xs ys (inj₂ y))
```

### `Any-∃`

```agda
Any-∃ : ∀ {A : Set} (P : A → Set) (xs : List A)
  → Any P xs ≃ ∃[ x ] (x ∈ xs × P x)
Any-∃ {A} P xs =
  record
    { to = to xs
    ; from = from xs
    ; from∘to = from∘to xs
    ; to∘from = to∘from xs
    }

  where

    to : ∀ (xs : List A)
      → Any P xs → ∃[ x ] (x ∈ xs × P x)
    to (x ∷ xs) (here Px) = ⟨ x , ⟨ (here refl) , Px ⟩ ⟩
    to (x ∷ xs) (there anyp) with to xs anyp
    ... | ⟨ x′ , ⟨ x′∈xs , Px′ ⟩ ⟩ = ⟨ x′ , ⟨ (there x′∈xs) , Px′ ⟩ ⟩

    from : ∀ (xs : List A)
      → ∃[ x ] (x ∈ xs × P x) → Any P xs
    from (x ∷ xs) ⟨ x′ , ⟨ here x′≡x , Px′ ⟩ ⟩ rewrite sym x′≡x = here Px′
    from (x ∷ xs) ⟨ x′ , ⟨ there x′∈xs , Px′ ⟩ ⟩ = there (from xs ⟨ x′ , ⟨ x′∈xs , Px′ ⟩ ⟩)

    from∘to : ∀ (xs : List A) (x : Any P xs) → from xs (to xs x) ≡ x
    from∘to (x ∷ xs) (here Px) = refl
    from∘to (x ∷ xs) (there anyp) = cong there (from∘to xs anyp)

    to∘from : ∀ (xs : List A) (y : ∃[ x ] (x ∈ xs × P x)) → to xs (from xs y) ≡ y
    to∘from (x ∷ xs) ⟨ .x , ⟨ here refl , Px′ ⟩ ⟩ = refl
    to∘from (x ∷ xs) ⟨ x′ , ⟨ there x′∈xs , Px′ ⟩ ⟩ =
      cong (λ{ ⟨ x″ , ⟨ x″∈xs , Px″ ⟩ ⟩ → ⟨ x″ , ⟨ (there x″∈xs) , Px″ ⟩ ⟩})
        (to∘from xs ⟨ x′ , ⟨ x′∈xs , Px′ ⟩ ⟩)
```

## `¬Any≃All¬`

`¬Any⇔All¬` 和 `¬∃⇔∀¬` 类似：

```agda
¬Any≃All¬ : ∀ {A : Set} (P : A → Set) (xs : List A)
  → (¬_ ∘ Any P) xs ≃ All (¬_ ∘ P) xs
¬Any≃All¬ P xs =
  record
    { to = to P xs
    ; from = from P xs
    ; from∘to = from∘to P xs
    ; to∘from = to∘from P xs
    }

  where

    to : ∀ {A : Set} (P : A → Set) (xs : List A)
      → (¬_ ∘ Any P) xs → All (¬_ ∘ P) xs
    to _ [] _ = []
    to P (x ∷ xs) ¬Any = (λ Px → ¬Any (here Px)) ∷ to P xs λ anyx → ¬Any (there anyx)

    from : ∀ {A : Set} (P : A → Set) (xs : List A)
      → All (¬_ ∘ P) xs → (¬_ ∘ Any P) xs
    from P [] [] ()
    from P (x ∷ xs) (¬Px ∷ All¬) (here Px) = ¬Px Px
    from P (x ∷ xs) (¬Px ∷ All¬) (there anyp) = from P xs All¬ anyp

    from∘to : ∀ {A : Set} (P : A → Set) (xs : List A) (x : (¬_ ∘ Any P) xs)
      → from P xs (to P xs x) ≡ x
    from∘to P [] ¬Any = extensionality λ ()
    from∘to P (x ∷ xs) ¬Any = extensionality
      λ{ (here Px) → refl
       ; (there anyp) → ⊥-elim (¬Any (there anyp))
       }

    to∘from : ∀ {A : Set} (P : A → Set) (xs : List A) (y : All (¬_ ∘ P) xs)
      → to P xs (from P xs y) ≡ y
    to∘from P [] [] = refl
    to∘from P (x ∷ xs) (¬Px ∷ All¬) = cong (¬Px ∷_) (to∘from P xs All¬)
```

值得注意的是，这里的 `Any P` 是 `Set₁` 而非 `Set₀` 的，这里的 `∘` 也是 universe polymorphic 的版本。

## `Bool` 版本的 `All` 和 `Any`

`All` 和 `Any` 也可以定义成 `Bool` 版本的：

```agda
all : ∀ {A : Set} → (A → Bool) → List A → Bool
all p  =  foldr _∧_ true ∘ map p

any : ∀ {A : Set} → (A → Bool) → List A → Bool
any p  =  foldr _∨_ true ∘ map p
```

## `Dec` 版本的 `All` 和 `Any`

为了能够得到 `Dec` 版本的 `All` 和 `Any`，首先需要获得谓词 `P` 对于某个元素的证明 `P x`。这里用 `Decidable` 来描述：

```agda
Decidable : ∀ {A : Set} → (A → Set) → Set
Decidable {A} P  =  ∀ (x : A) → Dec (P x)
```

然后就可以定义出 `All?` 和 `Any?`：

```agda
All? : ∀ {A : Set} {P : A → Set} → Decidable P → Decidable (All P)
All? P? []                                 =  yes []
All? P? (x ∷ xs) with P? x   | All? P? xs
...                 | yes Px | yes Pxs     =  yes (Px ∷ Pxs)
...                 | no ¬Px | _           =  no λ{ (Px ∷ Pxs) → ¬Px Px   }
...                 | _      | no ¬Pxs     =  no λ{ (Px ∷ Pxs) → ¬Pxs Pxs }
```

```agda
Any? : ∀ {A : Set} {P : A → Set} → Decidable P → Decidable (Any P)
Any? P? []                                = no (λ ())
Any? P? (x ∷ xs) with P? x   | Any? P? xs
...                 | yes Px | _          = yes (here Px)
...                 | no _   | yes Pxs    = yes (there Pxs)
...                 | no ¬Px | no ¬Pxs    = no λ{ (here Px)   → ¬Px Px
                                                ; (there Pxs) → ¬Pxs Pxs }
```

### `merge` 与 `split`

`merge` 能将两个列表合并成一个（类似归并的方式）。`split` 则相反，它接收一个谓词 `P`，并根据谓词 `P` 将一个列表分割成两个列表：

```agda
data merge {A : Set} : (xs ys zs : List A) → Set where
  [] :
      --------------
      merge [] [] []

  left-∷ : ∀ {x xs ys zs}
    → merge xs ys zs
      --------------------------
    → merge (x ∷ xs) ys (x ∷ zs)

  right-∷ : ∀ {y xs ys zs}
    → merge xs ys zs
      --------------------------
    → merge xs (y ∷ ys) (y ∷ zs)

_ : merge [ 1 , 4 ] [ 2 , 3 ] [ 1 , 2 , 3 , 4 ]
_ = left-∷ (right-∷ (right-∷ (left-∷ [])))

split : ∀ {A : Set} {P : A → Set} (P? : Decidable P) (zs : List A)
  → ∃[ xs ] ∃[ ys ] (merge xs ys zs × All P xs × All (¬_ ∘ P) ys)
split P? []                                                                 = ⟨ [] ,     ⟨ [] ,     ⟨ [] ,        ⟨ [] ,       [] ⟩ ⟩ ⟩ ⟩
split P? (z ∷ zs) with P? z   | split P? zs
...                  | yes Pz | ⟨ xs , ⟨ ys , ⟨ m , ⟨ Pxs , ¬Pys  ⟩  ⟩  ⟩ ⟩ = ⟨ z ∷ xs , ⟨ ys ,     ⟨ left-∷ m ,  ⟨ Pz ∷ Pxs , ¬Pys ⟩ ⟩ ⟩ ⟩
...                  | no ¬Pz | ⟨ xs , ⟨ ys , ⟨ m , ⟨ Pxs , ¬Pys  ⟩  ⟩  ⟩ ⟩ = ⟨ xs ,     ⟨ z ∷ ys , ⟨ right-∷ m , ⟨ Pxs ,      ¬Pz ∷ ¬Pys ⟩ ⟩ ⟩ ⟩
```
